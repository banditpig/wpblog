---
ID: 198
post_title: Simple Fractals in Haskell.
author: BanditPig
post_date: 2017-03-25 10:44:29
post_excerpt: ""
layout: post
permalink: >
  http://gitcommit.co.uk/2017/03/25/simple-fractals-in-haskell/
published: true
---
<img class="alignnone size-full wp-image-317" src="http://gitcommit.co.uk/wp-content/uploads/2017/03/refresh.png" alt="" width="128" height="128" />This posting was inspired by the ideas in <a href="http://web.cecs.pdx.edu/~mpj/pubs/composing-fractals.pdf">'Composing Fractals' by Mark P Jones.</a>
Fractals are generated by applying a function to the result of applying that function to the result of applying that function...
Haskell has a very simple way of expressing this iterative function application:
<pre class="lang:haskell decode:true ">iterate :: (a -&gt; a) -&gt; a -&gt; [a]</pre>
i.e. iterate takes a function, that takes an 'a' and produces an 'a', along with an initial 'a' - the seed value. The function is then repeatedly applied 'forever' giving an infinite list of [a].
<pre class="lang:haskell decode:true ">-- here x is the seed
iterate f x == [x, f x, f (f x), ...]</pre>
Of course we can't really use all of an infinite list so we apply Haskell's 'take' function to obtain however many items from the list.
For example in ghci:
<pre class="lang:haskell decode:true ">Prelude
λ-&gt; xs = iterate (+1) 0
Prelude
λ-&gt; take 5 xs
[0,1,2,3,4]
Prelude
λ-&gt; take 20 xs
[0,1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19]
Prelude</pre>
Two of the most well known fractals are the Mandelbrot set and the Julia set and both are generated in similar but subtly different ways. With the Mandelbrot set the sequence is calculated with a seed of 0 and for the Julia set the seed is the current point from the (x, y) grid being considered. In either case the values in the sequence could become larger and larger but if they don't then they are counted as 'being in the set'.
These ideas about sequences converging, seeds etc are explained in a very accessible way at <a href="https://plus.maths.org/content/unveiling-mandelbrot-set">https://plus.maths.org/content/unveiling-mandelbrot-set</a>
So let's see some code!
<pre class="lang:haskell decode:true ">{-# LANGUAGE OverloadedStrings #-}
{-# OPTIONS -Wall -fwarn-tabs -fno-warn-type-defaults -fno-warn-unused-do-bind #-}
-- The package juicypixels needs to have been installed.
import Data.Complex
import Codec.Picture

type C = Complex Double
type Pnt = (Double, Double)
type Grid a = [[a]]

w :: Int 
w = 500

h :: Int 
h = 500 

-- Complex constants for the Julia set rendering.
fixC1, fixC2, fixC3, fixC4 :: C
fixC1 = (-1.037) :+ 0.17
fixC2 = (-0.52) :+ 0.57 
fixC3 = 0.295 :+ 0.55 
fixC4 = (-0.624) :+ 0.435

juliaConstants :: [(Int, C)]
juliaConstants =[ (1, fixC1), (2, fixC2), (3, fixC3), (4, fixC4)]


--iterate f x == [x, f x, f (f x), ...]
-- in mandel the seed is fixed at 0 
mandel :: C -&gt; [C]
mandel c = iterate (\z -&gt; z * z + c)  (0.0 :+ 0.0)

--iterate f x == [x, f x, f (f x), ...]
-- in julia, for a given fixed constant,  the seed varies
julia :: C -&gt;  C -&gt; [C]
julia fixc  = iterate (\z -&gt; z * z + fixc)  

closeToZero :: C -&gt; Bool
closeToZero z = magnitude z &lt; 5

-- get the pallete value corresponding to the 'depth' at which the value fails the test 
chooseColor  :: [color] -&gt; ( C -&gt; [C]) -&gt; C -&gt; color
chooseColor colors fz z = (colors !!) . length . take n . takeWhile closeToZero $ fz z
  where n = length colors - 1

-- A grid of points that is defined by its  bottom left, top right and the number of subdivision per row and per column
grid :: Int -&gt; Int -&gt; Pnt -&gt; Pnt -&gt; Grid Pnt
grid c r (xmin, ymin) (xmax, ymax ) = [[(x, y) | x &lt;- for c xmin xmax ] | y &lt;- for r ymin ymax ] where
    for :: Int -&gt;Double -&gt;Double -&gt;[Double]
    for n mn mx = take n [mn, mn + delta ..]
     where delta = (mx - mn) / fromIntegral (n - 1)

-- a grid
grid1 :: Grid Pnt
grid1 = grid w h (-1.5, -1.5) (1.5, 1.5)

-- calculate sequences over the grid and for each sequence get an 'end' colour
mapOverGrid :: Grid Pnt -&gt; (C -&gt; [C]) -&gt; [color] -&gt; [[color]]
mapOverGrid grd fz colors = [ [chooseColor colors  fz  (x :+ y) | (x,y) &lt;- row] | row &lt;- grd ]

-- used by juicy pixels to get a pixel value at x, y
gridFunc ::Grid Pnt -&gt;  (C -&gt; [C]) -&gt; Int -&gt; Int -&gt; PixelRGB8
gridFunc grd fz  x y = ((mapOverGrid grd fz rgb8) !! y) !! x

generateImg :: (Int -&gt; Int -&gt; PixelRGB8) -&gt; DynamicImage
generateImg gf = ImageRGB8 (generateImage gf w h)

-- Juicy pixels type 
rgb8 :: [PixelRGB8]
rgb8 = foldr (\x ac  -&gt; PixelRGB8 x x x  : ac) [] [255, 254..0]

allJulia :: IO()
allJulia = 
    mapM_ makeSaveImage juliaConstants where 
        makeSaveImage :: (Int, C) -&gt; IO()
        makeSaveImage (n, c) = 
         savePngImage name $ generateImg . gridFunc grid1 . julia $ c where 
            name = "imageJ" ++ show n ++ ".png" 

main :: IO ()
main = do
    allJulia
    savePngImage "imageM.png" $ generateImg . gridFunc grid1 $ mandel
    print "Done."
</pre>
I think the code is fairly self explanatory and quite succinct given what it does.
The images below have been generated by the above code and they are the Mandelbrot and the four Julia images corresponding to 'fixC1' to 'fixC4'. (The background colour has been manually changed to contrast with the page background).

[caption id="attachment_222" align="alignnone" width="300"]<img class="size-medium wp-image-222" src="http://gitcommit.co.uk/wp-content/uploads/2017/03/imageM-1-300x300.png" alt="" width="300" height="300" /> Mandelbrot[/caption]

[caption id="attachment_226" align="alignnone" width="300"]<img class="size-medium wp-image-226" src="http://gitcommit.co.uk/wp-content/uploads/2017/03/imageJ1-300x300.png" alt="" width="300" height="300" /> Julia 1[/caption]

[caption id="attachment_227" align="alignnone" width="300"]<img class="size-medium wp-image-227" src="http://gitcommit.co.uk/wp-content/uploads/2017/03/imageJ2-300x300.png" alt="" width="300" height="300" /> Julia 2[/caption]

[caption id="attachment_228" align="alignnone" width="300"]<img class="size-medium wp-image-228" src="http://gitcommit.co.uk/wp-content/uploads/2017/03/imageJ3-300x300.png" alt="" width="300" height="300" /> Julia 3[/caption]

[caption id="attachment_229" align="alignnone" width="300"]<img class="size-medium wp-image-229" src="http://gitcommit.co.uk/wp-content/uploads/2017/03/imageJ4-300x300.png" alt="" width="300" height="300" /> Julia 4[/caption]